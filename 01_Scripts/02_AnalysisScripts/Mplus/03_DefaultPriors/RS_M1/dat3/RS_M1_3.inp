TITLE: M1 dat3 Default Priors;

DATA: 
FILE = mlsem_dat3.dat;

VARIABLE: 
NAMES = country imprel confidence belong attend pray person year code RCCHR RCMUSLIM
     RCASIAN RCOTHER COMMALL RRI RLI ZAFRICA ZLA ZINDIC ZSINIC ZNWEST ZISLAM ZORT
     ZEUWEST; 
USEVARIABLES = country imprel person confidence pray attend belong 
     RCASIAN RCOTHER COMMALL RRI RLI;
MISSING = .;

BETWEEN = RCASIAN RCOTHER COMMALL RRI RLI;
CLUSTER = country;
CATEGORICAL = imprel confidence belong person;

ANALYSIS: 
ESTIMATOR = BAYES;
PROCESSORS = 2; 
TYPE = TWOLEVEL RANDOM; 
THIN = 20;
FBITERATIONS = 10000;

MODEL: 
%WITHIN%
Rel_W BY imprel person confidence pray belong;
Res1_W BY pray@1 attend@1;
Res2_W BY person@1 belong@1;
Res1_W WITH Rel_W@0;
Res2_W WITH Rel_W@0;
Res1_W WITH Res2_W@0;

SLOPE | attend ON Rel_W;

%BETWEEN%
Rel_B BY imprel person confidence pray attend belong;
Res1_B BY pray@1 attend@1;
Res2_B BY person@1 belong@1;
Res1_B WITH Rel_B@0;
Res2_B WITH Rel_B@0;
Res1_B WITH Res2_B@0;

SLOPE ON RCASIAN RCOTHER COMMALL RRI RLI;

OUTPUT: 
TECH8;

PLOT: 
TYPE = PLOT2;

SAVEDATA:
BPARAMETERS = parameters.dat;
